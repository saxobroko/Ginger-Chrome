# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")

polymer_element_files = [
  "wallpaper_collections_element.js",
  "wallpaper_images_element.js",
  "wallpaper_selected_element.js",
  "personalization_router_element.js",
]

static_files = [
  "index.html",
  "mojo_interface_provider.js",
  "personalization_actions.js",
  "personalization_app.js",
  "personalization_store.js",
  "styles.js",
]

js_library("mojo_interface_provider") {
  deps = [
    "../../mojom:mojom_js_library_for_compile",
    "../common:utils",
  ]
}

js_library("personalization_actions") {
  deps = [ "//ui/webui/resources/js/cr/ui:store.m" ]
}

js_library("personalization_app") {
  deps = [
    ":personalization_router_element",
    ":styles",
    ":wallpaper_collections_element",
    ":wallpaper_images_element",
    ":wallpaper_selected_element",
  ]
}

js_library("personalization_router_element") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/iron-location:iron-location",
    "//third_party/polymer/v3_0/components-chromium/iron-location:iron-query-params",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("personalization_store") {
  deps = [
    ":personalization_actions",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js/cr/ui:store.m",
    "//ui/webui/resources/js/cr/ui:store_client.m",
  ]
}

js_library("styles") {
}

js_library("wallpaper_collections_element") {
  deps = [
    ":mojo_interface_provider",
    ":styles",
    "../common:constants",
    "//third_party/polymer/v3_0/components-chromium/paper-spinner:paper-spinner-lite",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("wallpaper_images_element") {
  deps = [
    ":mojo_interface_provider",
    ":styles",
    "../common:constants",
    "//third_party/polymer/v3_0/components-chromium/paper-spinner:paper-spinner-lite",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("wallpaper_selected_element") {
  deps = [
    ":mojo_interface_provider",
    ":personalization_store",
    ":styles",
    "//third_party/polymer/v3_0/components-chromium/paper-spinner:paper-spinner-lite",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_type_check("closure_compile") {
  is_polymer3 = true
  deps = [
    ":mojo_interface_provider",
    ":personalization_actions",
    ":personalization_app",
    ":personalization_router_element",
    ":personalization_store",
    ":styles",
    ":wallpaper_collections_element",
    ":wallpaper_images_element",
    ":wallpaper_selected_element",
  ]
}

html_to_js("web_components") {
  js_files = polymer_element_files
}

copy("copy_static") {
  sources = static_files
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

copy("copy_mojo_to_trusted") {
  deps = [ "../../mojom:mojom_js" ]
  sources = [ "$target_gen_dir/../../mojom/personalization_app.mojom-lite.js" ]
  outputs = [ "$target_gen_dir/personalization_app.mojom-lite.js" ]
}

preprocess_if_expr("preprocess") {
  deps = [
    ":copy_mojo_to_trusted",
    ":copy_static",
    ":web_components",
  ]
  in_folder = "$target_gen_dir/.."
  out_folder = "$target_gen_dir/processed"
  out_manifest = "$target_gen_dir/manifest.json"
  in_files = []
  foreach(file, polymer_element_files) {
    in_files += [ "trusted/$file" ]
  }
  foreach(file, static_files) {
    in_files += [ "trusted/$file" ]
  }
  in_files += [ "trusted/personalization_app.mojom-lite.js" ]
}
